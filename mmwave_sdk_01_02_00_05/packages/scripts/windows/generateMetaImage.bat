@echo off
REM
REM
REM
REM Prerequiste: Ensure that the MMWAVE_SDK_INSTALL_PATH has been setup properly before calling the script.
REM 
REM
REM
REM ================================================
REM generateMetaImage.bat <FLASHIMAGE> <SHMEM_ALLOC> <MSS_IMAGE_BIN> <BSS_IMAGE_BIN> <DSS_IMAGE_BIN>
REM 		where
REM 			FLASHIMAGE: 	[output] multicore file that will be generated by this script and should be used for flashing onto the board
REM			SHMEM_ALLOC: 	[input]  shared memory allocation in 32-bit hex format where each byte (left to right) is for BSS,TCMB,TCMA,DSS
REM			MSS_IMAGE: 	[input]  MSS input image in RPRC (,bin) format , use keyword NULL if not needed
REM			BSS_IMAGE: 	[input]  BSS input image in RPRC (,bin) format, use keyword NULL if not needed
REM			DSS_IMAGE: 	[input]  DSP input image, use keyword NULL if not needed
REM
REM		example:
REM			generateMetaImage.bat metaImage.bin 0x01000005 xwr16xx_mmw_demo_mss.bin xwr1xxx_bss.bin xwr16xx_mmw_demo_dss.bin
REM ================================================

set IMAGE_CREATOR_DIR=xwr14xx
if [%MMWAVE_SDK_DEVICE%] == [awr16xx] (
    set IMAGE_CREATOR_DIR=xwr16xx
)
if [%MMWAVE_SDK_DEVICE%] == [iwr16xx] (
    set IMAGE_CREATOR_DIR=xwr16xx
)

REM ================================================
REM tools required
SET MULTCOREGEN=%MMWAVE_SDK_INSTALL_PATH%\scripts\ImageCreator\%IMAGE_CREATOR_DIR%\multicore_image_generator\MulticoreImageGen.exe
SET CRC_MULTI=%MMWAVE_SDK_INSTALL_PATH%\scripts\ImageCreator\%IMAGE_CREATOR_DIR%\crc_multicore_image\crc_multicore_image.exe
SET CRC_EXE=%MMWAVE_SDK_INSTALL_PATH%\scripts\ImageCreator\append_bin_crc\gen_bincrc32.exe
REM ================================================


REM Calculate the number of arguments
SET /A ARGS_COUNT=0
FOR %%A in (%*) DO SET /A ARGS_COUNT+=1
if %ARGS_COUNT% lss 3 (
	echo Error: Invalid Usage
	echo        generateMetaImage.bat FLASHIMAGE SHMEM_ALLOC MSS_IMAGE_BIN BSS_IMAGE_BIN DSS_IMAGE_BIN
	goto end
)

SET FLASHIMAGE=%1
SET SHMEM_ALLOC=%2
SET MSS_IMAGE_BIN=%3
SET BSS_IMAGE_BIN=%4
SET DSS_IMAGE_BIN=%5

SET TEMP_FILE=%FLASHIMAGE%.tmp

IF EXIST %MSS_IMAGE_BIN% (SET "MSS_CORE=0x35510000 %MSS_IMAGE_BIN%") ELSE SET "MSS_CORE="
IF EXIST %BSS_IMAGE_BIN% (SET "BSS_CORE=0xb5510000 %BSS_IMAGE_BIN%") ELSE SET "BSS_CORE="
IF EXIST %DSS_IMAGE_BIN% (SET "DSS_CORE=0xd5510000 %DSS_IMAGE_BIN%") ELSE SET "DSS_CORE="

REM generate the multicore image
REM format:  MulticoreImageGen.exe <LE/BE> <DEV_ID> <SHMEM_ALLOC> <OUTPUT FILE> <COREID> <IMAGE1> <COREID> <IMAGE2> ..
echo %MULTCOREGEN% LE 37 %SHMEM_ALLOC% %FLASHIMAGE% %MSS_CORE% %BSS_CORE% %DSS_CORE%
call %MULTCOREGEN% LE 37 %SHMEM_ALLOC% %FLASHIMAGE% %MSS_CORE% %BSS_CORE% %DSS_CORE%

REM generate and fill in the CRC for the multicore images
echo call %CRC_MULTI% %FLASHIMAGE% %TEMP_FILE%
call %CRC_MULTI% %FLASHIMAGE% %TEMP_FILE%

REM append crc to the flash image
echo call %CRC_EXE% %FLASHIMAGE%
call %CRC_EXE% %FLASHIMAGE%

:end

